//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2015  Hercules Dev Team
//= Copyright (C)  Euphy
//= Copyright (C)  Kisuka
//= Copyright (C)  L0ne_W0lf
//= Copyright (C)  Lupus
//= Copyright (C)  DZeroX
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Alberta Town
//================= Description ===========================================
//= Town-specific Alberta NPCs
//================= Current Version =======================================
//= 1.6
//=========================================================================

alberta,97,51,0	script	Fabian	4_M_02,{
	mes "[Fabian]";
	mes "Quando você viaja para muitos lugares como eu faço, você fica sabendo de muitos rumores. Um dos rumores que eu ouvi é realmente interessante.";
	next;
	mes "[Fabian]";
	mes "Aparentemente existem cartas que selam o poder de certos monstros dentro delas.";
	mes "Se alguém conseguir obter uma dessas cartas, ele irá obter o poder do monstro...";
	next;
	mes "[Fabian]";
	mes "Ah! Eu não acredito nisso.";
	close;
}

alberta,53,39,0	script	Steiner	4W_SAILOR,{
	mes "[Steiner]";
	mes "Oh, você parece ser de fora da cidade. Bem vindo à Alberta.";
	emotion 0;
	next;
	mes "[Steiner]";
	mes "Eu estava apenas pensando em algumas idéias que eu tenho para o meu negócio.";
	next;
	mes "[Steiner]"; 
	mes "Olha só, eu ouvi falar que tem uma loja em Geffen que vende armaduras raras, resistentes a ataques mágicos."; 
	mes "Se eu de alguma forma pudesse comprá-las por um preço baixo e vendê-las pro pessoal em outras cidades...";
	next;
	mes "[Steiner]";
	mes "Aaah! (você vê cifrões estampados nos olhos dele)";
	emotion 8;
	close;
}

alberta,20,183,0	script	Chad	1_M_03,{
	mes "[Chad]";
	mes "As pessoas dizem que a lendária arma Gungnir nunca erra seu alvo. Eu me pergunto se isso é verdade...";
	next;
	mes "[Chad]";
	mes "As pessoas também dizem que as cegonhas é que trazem os bebês depois do parto, e que Papai Noel existe! Mas só em Lutie.";
	next;
	mes "[Chad]";
	mes "Eu me pergunto...";
	mes "Se qualquer uma dessas pessoas realmente acredita que isso seja verdade...";
	close;
}

alberta,131,139,2	script	Velho Bêbado	1_M_JOBGUIDER,{
	mes "[Velho Bêbado]";
	mes "^666666*Hiccup*^000000";
	mes "O quqquê você está olhando?";
	mes "Sai fora!!";
	next;
	switch(select("Não, vou ficar aqui mesmo.","Tah tah! Seu velho bêbado.")) {
		case 1:
			mes "[Velho Bêbado]";
			mes "Hahahaha ^666666*hiccup*^000000... Então você tem nervos. Heh... eu posso parecer um lixo agora, mas voltando no tempo, eu era um belo marinheiro à bordo do 'Going Mary.'";
			next;
			switch(select("Isso é um navio?.","Sério? Não brinca!")) {
				case 1:
					mes "[Velho Bêbado]";
					mes "O quê? Nunca ouviu falar dele? Idiota! Todo mundo conhece o notável navio pirata 'Going Mary!' ^666666*Hiccup~*^000000";
					next;
					mes "[Velho Bêbado]";
					mes "Ah~ bons dias ... mas... se nós não tivéssemos entrado naquela TEMPESTADE...^666666*hiccup*^000000";
					next;
					mes "[Velho Bêbado]";
					mes "AH~ Capitão. Sinto falta do nosso capitão mais do que tudo.... nenhum monstro sobrevivia ao golpe da espada do capitão."; 
					mes "CAPITÃO~~~!!! (~HICCUP~) Ele usava sua espada ASSIM!... depois ASSIM... e ASSIM...!!!"; 
					next; 
					mes "[Velho Bêbado]"; 
					mes "O inimigo e tudo ao redor dele ardiam em chamas! Agora quando penso nisso, a espada devia ter algum tipo de poder misterioso."; 
					next; 
					mes "[Velho Bêbado]"; 
					mes "(~sigh!~) (~sob, sob~)... Desu, como eu sinto falta de todos... Agora me deprimi. Vá embora e me deixe sozinho....."; 
					emotion 28; 
					close; 
				case 2:
					mes "[Velho Bêbado]";
					mes "Está certo! (~HICCUP~)!";
					close;
			}
		case 2:
			mes "[Velho Bêbado]";
			mes "É isso mesmo! Vá EMBORA!"; 
			emotion 32; 
			close;
	}
}

alberta,58,80,2	script	Shakir	4W_M_03,{
	mes "[Shakir]";
	if (rand(2)) {
		mes "Nós mercadores podemos fazer nosso comércio em qualquer lugar. Com o ^0000ffDisconto^000000 nós podemos comprar tudo na loja com preço mais baixo..";
		next;
		mes "[Shakir]";
		mes "É mais do que apenas sair gritando 'Você tem que me dar mais dinheiro por favor!' Você precisa ter carisma e retórica de mestre!";
		next;
		mes "[Shakir]";
		mes "A porcentagem máxima que conseguimos tirar a mais nos itens que vendemos aos NPCs é de 24%, mas precisa de trabalho duro e treino para conseguir essa habilidade!!";
	} else {
		mes "Nós, mercadores muitas vezes colocamos o pé na estrada para fazer bons negócio";
		next;
		mes "[Shakir]";
		mes "Com a habilidade de desconto, podemos comprar produtos mais baratos das lojas das cidades e carregá-los em nosso carrinho alugado.";
		next;
		mes "[Shakir]";
		mes "Então, depois disso, podemos viajar para qualquer lugar, e re-vender esses itens para lucrar um pouco!";
		next;
		mes "[Shakir]";
		mes "Desta forma, o negócio é mais conveniente e seguro.... EI NÃO DURMA!.";
	}
	close;
}

alberta,62,156,2	script	Sonya	8_F,{
	mes "[Sonya]";
	switch(rand(3)){
	case 0:
		mes "Uns dias atrás, eu estava andando na floresta e vi um longo, fino pedaço de grama verde saindo direto do chão.";
		next;
		mes "[Sonya]";
		mes "Ele era tãããão bonitinho, que me deu vontade de tocá-lo. Quando eu encostei nele, sabe o que aconteceu? A grama deu uma chicotada na minha mão!";
		next;
		mes "[Sonya]";
		mes "Achei aquilo estranho, e recuei por um momento. Foi então que eu vi, não era grama, mas sim uma pequena criatura.";
		next;
		mes "[Sonya]";
		mes "Até os montros mais calmos podem ser muito perigosos quando se sentem ameaçados. Então não se engane com eles.";
		close;
	case 1:
		mes "Sabe aqueles ursos com cara de bobo que vivem na floresta que liga Alberta à Payon??";
		mes "Claro que sabe... aqueles que ficam brincando com as abelhas que voam nos seus narizes";
		next;
		mes "[Sonya]";
		mes "Então, outro dia eu joguei um galho em um deles apenas por diversão. De repente, ELE VEIO CORRENDO NA MINHA DIREÇÃO!";
		next;
		mes "[Sonya]";
		mes "Eu fiquei TÃÃÃO assustada!! Eu rapidinho pulei pro lado pra desviar dele. Então BAMB!!! Ele acertou uma gigantesca árvore e a deixou em pedacinhos.";
		emotion 16;
		next;
		mes "[Sonya]";
		mes "Aquele dia eu aprendi uma lição. NUNCA provoque nenhuma criatura pois se ela ficar nervosa, você estará em sérios apuros!";
		close;
	case 2:
		mes "Uma vez eu vi um bando de lobos pegarem um daqueles enormes, ursos preguiçosos!";
		next;
		mes "[Sonya]";
		mes "Você sabia? Lobos são mais cooperativos do que aparentam. Se um deles é atacado, os outros vêm ajudá-lo.";
		next;
		mes "[Sonya]";
		mes "Então tome muito cuidado se você decidir lutar com um!";
		close;
	}
}

alberta,93,174,2	script	Vovó Alma	8_F_GRANDMOTHER,{
	mes "[Vovó Alma]";
	mes "Algum tempo atrás, um navio à deriva foi trazido pelo mar ao porto de Alberta.";
	next;
	mes "[Vovó Alma]";
	mes "Alguns jovens da cidade entraram no navio em busca de sobreviventes. Mas depois de alguns momentos todos correram pra fora horrorizados. Disseram que viram CADÁVERES andando pelo navio!";
	next;
	mes "[Vovó Alma]";
	mes "O navio também estava lotado de monstros marinhos nunca vistos, que impediam as pessoas de entrar.";
	next;
	mes "[Vovó Alma]";
	mes "Nós nunca fomos capazes de fazer algo sobre esse estranho navio, então apenas o deixamos lá, esperando que um dia ele afundasse ou fosse embora.";
	next;
	mes "[Vovó Alma]";
	mes "Assim, pode ser uma boa experiência para um jovem aventureiro como você. Mas, ainda não vale a pena arriscar sua vida se você não é forte o suficiente.";
	close;
}

alberta,189,151,5	script	Marinheiro Fisk	4W_SAILOR,{
	mes "[Marinheiro Fisk]";
	mes "Olá, pra onde deseja ir?";
	next;
	switch(select("Navio Fantasma -> 250 zeny.","Marinha de Izlude -> 500 zeny.","Sair.")) {
		case 1:
			if (Zeny < 250) {
				mes "[Marinheiro Fisk]";
				mes "Eu disse que preciso de 250 Zeny, e parece que você não tem.";
				close;
			}
			Zeny -= 250;
			warp "alb2trea",43,53;
			end;
		case 2:
			if (Zeny < 500) {
				mes "[Marinheiro Fisk]";
				mes "Eu disse que preciso de 500 Zeny, e parece que você não tem.";
				close;
			}
			Zeny -= 500;
			if (RENEWAL)
				warp "izlude",195,212;
			else
				warp "izlude",176,182;
			end;
		case 3:
			mes "[Marinheiro Fisk]";
			mes "Bem...";
			mes "Você parece mesmo uma pessoa que gosta de terra firme.";
			close;
	}
}

alb2trea,39,50,6	script	Fisk#a2t	4W_SAILOR,{
	mes "[Marinheiro Fisk]";
	mes "Então você colocou na cabeça que quer volta para o continente de Alberta, eh?";
	next;
	if(select("Sim por favor.","Não, Mudei de idéia.")==1)
		warp "alberta",192,169;
	close;
}

alberta,195,151,2	script	Paul	4_M_04,{
	mes "[Paul]";
	mes "Bom dia~";
	mes "Você gostaria de juntar-se ao time de exploração do Navio Fantasma?";
	next;
	mes "[Paul]";
	mes "Oh! Melhor lhe alertar, esse evento é recomendado para guerreiros de ALTO NÍVEL.";
	next;
	mes "[Paul]";
	mes "Então o que diz? Apenas 200 zeny para participar e você vai ter uma tonelada de experiência, eu garanto.";
	next;
	switch(select("Inscreva-me!","Uh, não obrigado.")) {
		case 1:
			if (Zeny < 200) {
				mes "[Paul]";
				mes "Parece que você não tem o dinheiro, meu amigo. Por favor, volte quando for capaz de pagar.";
				close;
			} else {
				Zeny -= 200;
				warp "alb2trea",62,69;
				close;
			}
		case 2:
			mes "[Paul]";
			mes "Tudo bem, bem... eu estarei por perto se você mudar de idéia";
			close;
	}
}

alberta,190,173,4	script	Phelix	4_M_03,{
	.@weight = MaxWeight-Weight;
	mes "[Phelix]";
	if ((.@weight) < 10000) {
		mes "Espere um momento!!";
		mes "Você trouxe muitas coisas!";
		mes "Você está acima do limite de peso permitido!";
		mes "Por favor, reduza a quantidade de itens, em seguida, venha me ver novamente";
		close;
	}
	if (@event_zelopy == 0) {
		mes "O que diabos você está fazendo aqui?";
		mes "Fique sabendo que Não há nada que consiga de graça nesse navio, se quer ser recompensado, trabalhe!!";
		next;
		mes "[Phelix]";
		mes "Hmm, em todo caso, estou trocando alguns item por jellopys";
		mes " - Pra ^0000ff10 Jellopys^000000 Eu te dou ^ff00001 Poção Vermelha^000000."; 
		mes " - Pra ^0000ff3 Jellopys^000000 Eu te dou ^ff00001 Cenoura^000000."; 
		next;
		mes "[Phelix]";
		mes "Caso esteja interessado em minha oferta, traga-me as coisas mencionadas.";
		@event_zelopy = 1;
		close;
	} else {
		mes "E então... O que você quer por seus jellopys?";
		next;
		switch(select("Poções Vermelhas, por favor.","Cenouras, por favor.")) {
		case 1:
			mes "[Phelix]";
			mes "Bem...";
			mes "Vamos ver o que você tem ai...";
			next;
			mes "[Phelix]";
			if (countitem(909) < 10) {
				mes "Ei! Você não estava ouvindo ?. Eu disse 10 jellopys por 1 Poção Vermelha. Você é surdo?";
				close;
			} else {
				.@max = countitem(909)/10;
				mes "Hmm, nada mal...";
				mes "Quantas poções você quer?";
				next;
				switch(select("O Máximo que puder, por favor.","Eu quero muitas.","Deixa pra lá, Gosto dos meus jellopys.")) {
				case 1:
					delitem 909,.@max*10;	// Jellopy
					getitem 501,.@max;	// Poção_Vermelha
					break;
				case 2:
					mes "[Phelix]";
					mes "Coloque a quantidade. Coloque 0 pra cancelar.";
					mes "Neste momento, o máximo que você poderá pegar é " + .@max + " e por favor, não peça mais de 100... você quer quebrar minhas costas?.";
					input .@amount;
					next;
					mes "[Phelix]";
					if (.@amount <= 0) {
						mes "Muito obrigado, venha novamente a qualquer hora.";
						close;
					}
					if (.@amount > 100) {
						mes "Hey, O que eu disse? 100 de cada vez, você está tentando me matar ???";
						close;
					}
					if (countitem(909) < .@amount*10) {
						mes "Hmm, parece que você não tem o suficiente. Vá buscar mais alguns Jellopys se você quiser algo de mim.";
						close;
					}
					delitem 909,.@amount*10;	// Jellopy
					getitem 501,.@amount;	// Poção_Vermelha
					break;
				case 3:
					mes "[Phelix]";
					mes "Sem problema, vejo você a qualquer hora";
					close;
				}
				mes "[Phelix]";
				mes "Ai está! Como eu prometi. Não vá tomar tudo de uma só vez.";
				close;
			}
		case 2:
			mes "[Phelix]";
			mes "Tudo bem, vamos ver o que você tem ai...";
			next;
			mes "[Phelix]";
			if (countitem(909) < 3) {
				mes "Hmm, olhe, eu disse 3 Jellopys por 1 Cenoura.. entendeu?";
				close;
			} else {
				.@max = countitem(909)/3;
				mes "Nada mal...";
				mes "Quantos você quer?";
				next;
				switch(select("O Máximo que puder, por favor","Quero muitos.","Deixa pra lá, Gosto de meus jellopys.")) {
				case 1:
					delitem 909,.@max*3;	// Jellopy
					getitem 515,.@max;	// Cenoura
					break;
				case 2:
					mes "[Phelix]";
					mes "Coloque a quantidade. Coloque 0 pra cancelar.";
					mes "e por favor, não peça mais de 100... vou acabar com minhas costas carregando peso.";
					input .@amount;
					next;
					mes "[Phelix]";
					if (.@amount == 0) {
						mes "Certo, então, vejo você por ai.";
						close;
					}
					if (.@amount > 100) {
						mes "Hey, O que eu disse? 100 de cada vez, você está tentando me matar ?!";
						close;
					}
					if (countitem(909) < .@amount*10) {
						mes "Parece que você não tem o suficiente. Vá pegar mais Jellopys se você quiser algo comigo.";
						close;
					}
					delitem 909,.@amount*3;	// Jellopy
					getitem 515,.@amount;	// Cenoura
					break;
				case 3:
					mes "[Phelix]";
					mes "Até mais tarde.";
					close;
				}
				mes "[Phelix]";
				mes "Aqui está~! Como prometido. Tente não comer tudo de vez.";
				close;
			}
		}
	}
}
